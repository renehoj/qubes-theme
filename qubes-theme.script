scr.w = Window.GetWidth();
scr.h = Window.GetHeight();
scr.half.w = scr.w / 2;
scr.half.h = scr.h / 2;

inputs = null;
imputs.key_press = 0;

bootProgress = null;
bootProgress.enabled = 1;
bootProgress.show = 0;
bootProgress.mode = Plymouth.GetMode();
bootProgress.text = "Progress (" + bootProgress.mode  + "): ";
bootProgress.image = Image.Text(bootProgress.text + "00%", 1, 1, 1);
bootProgress.sprite = Sprite();
bootProgress.sprite.SetX(scr.half.w - bootProgress.image.GetWidth() / 2);
bootProgress.sprite.SetY(scr.h - 8 * bootProgress.image.GetHeight());
bootProgress.value = 0;

message = null;
prompt = null;
bullets = null;
bullet.image = Image.Text("*", 1, 1, 1);

animation = null;
animation.index = 1;
animation.index_max = 119;
animation.delay = 0.05;
animation.time = 0;

for(i = 1; i < animation.index_max; i++) {
	animation.frames[i].image = Image("animation-" + i + ".png");
}

animation.sprite = Sprite();
animation.sprite.SetX(scr.half.w - animation.frames[1].image.GetWidth() / 2);
animation.sprite.SetY(scr.half.h - animation.frames[1].image.GetHeight() / 2);

logo = null;
logo.image = Image("qubes.png");
logo.sprite = Sprite(logo.image);
logo.sprite.SetX(scr.half.w - logo.image.GetWidth() / 2);
logo.sprite.SetY(scr.h - 25 - logo.image.GetHeight()); 
logo.opacity = 1;
logo.fade = 1;

statusPrompt = null;
statusPrompt.enabled = 0;
statusPrompt.show = 0;
statusPrompt.text = "";
statusPrompt.sprite = Sprite();

fun UpdateStatusFunction(status) {

	if(statusPrompt.enabled == 1 && status != null && status != "") {
		statusPrompt.image = Image.Text(status, 1, 1, 1);
		statusPrompt.sprite.SetX(scr.half.w - statusPrompt.image.GetWidth() / 2);
		statusPrompt.sprite.SetY(scr.h - 10 * statusPrompt.image.GetHeight());
		statusPrompt.sprite.SetImage(statusPrompt.image);
	}
}

Plymouth.SetUpdateStatusFunction(UpdateStatusFunction);

fun DoAnimation() {

	if(animation.index < animation.index_max) {
		animation.sprite.SetImage(animation.frames[animation.index].image);
		animation.index++;
	} else {
		animation.index = 1;
	}
}

fun RefreshFunction() {
	
	if(animation.delay == 0) {
		DoAnimation();
	}
}

Plymouth.SetRefreshFunction(RefreshFunction);

fun BootProgressText(progress) {

	pVal = Math.Int(progress * 100);
	
	if(bootProgress.show == 1 && pVal > bootProgress.value) {	
		bootProgress.value = pVal;
		bootProgress.image = Image.Text(bootProgress.text + pVal + "%", 1, 1, 1);
		bootProgress.sprite.SetImage(bootProgress.image);
	}
}

fun BootProgressFunction(time, progress) {

	if(bootProgress.enabled == 1) {		
		BootProgressText(progress);
	}

	if(logo.fade == 1 && time > 3 && logo.opacity > 0) {
		logo.opacity -= 0.05;
		logo.sprite.SetOpacity(logo.opacity);
	}

	if(animation.time == 0) {
		animation.time = time;
	}

	if(animation.delay > 0 && time - animation.time > animation.delay) {
		animation.time = time;
		DoAnimation();
	}
}

Plymouth.SetBootProgressFunction(BootProgressFunction);

fun PasswordFunction(nil, bulletCount) {

	if(statusPrompt.show == 1) {
		statusPrompt.sprite.SetOpacity(0);
		statusPrompt.show = 0;
	}	

	if(bootProgress.show == 1) {
		bootProgress.sprite.SetOpacity(0);
		bootProgress.show = 0;
	}

	tWidth = bulletCount * bullet.image.GetWidth();
	sPos = scr.half.w - tWidth / 2;
	
	prompt.image = Image.Text("Enter LUKS password", 1, 1, 1);
	prompt.sprite = Sprite(prompt.image);
	prompt.sprite.SetX(scr.half.w - prompt.image.GetWidth() / 2);
	prompt.sprite.SetY(scr.h - 12 * prompt.image.GetHeight());

	bullets = null;
	for(i = 0; i < bulletCount; i++){
		bullets[i].sprite = Sprite(bullet.image);
		bullets[i].sprite.SetX(sPos + i * bullet.image.GetWidth());
		bullets[i].sprite.SetY(scr.h - 10 * bullet.image.GetHeight());
	}
}

Plymouth.SetDisplayPasswordFunction(PasswordFunction);

fun DisplayNormalFunction() {
	
	bootProgress.show = 1;
	bootProgress.sprite.SetOpacity(1);
	statusPrompt.show = 1;
	statusPrompt.sprite.SetOpacity(1);

	bullets = null;
	prompt = null;
	message = null;
}

Plymouth.SetDisplayNormalFunction(DisplayNormalFunction);


fun KeyInputFunction(key) {
	inputs.key_press++;
}

Plymouth.SetKeyboardInputFunction(KeyInputFunction);


fun MessageFunction(text) {
	message.image = Image.Text(text, 1, 1, 1);
	message.sprite = Sprite(message.image);
	message.sprite.SetPosition(src.half.w - message.image.GetWidth() /2, 50);
}

Plymouth.SetMessageFunction(MessageFunction);

